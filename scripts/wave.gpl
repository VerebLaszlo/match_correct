#!/usr/bin/gnuplot -persist
# initialization & functions ======================================================================

getTitle(fileName) = word(system("head -n 8 ".fileName." | tail -n 1")[header:*],1).", ".word(system("head -n 7 ".fileName." | tail -n 1")[header:*],1)
file="wave.".type
files=system("ls -1 *".file)

# wave ============================================================================================

set key inside left bottom vertical
set xlabel "t[s]"
set ylabel "h[10^{-20}]"; set ytics format "%3.2t"; set yra [-9.9e-20:+9.9e-20]

set output "wave.eps"
do for [file in files] {
	method=system("head -n 7 ".file." | tail -n 1")[header:*]
	amp=word(method,3)/2.0
	method1=sprintf("%s %g %s", word(method,1)." kölcsönhatás ",amp,"PN amplitúdóval")
	method=system("head -n 8 ".file." | tail -n 1")[header:*]
	amp=word(method,3)/2.0
	method2=sprintf("%s %g %s", word(method,1)." kölcsönhatás ",amp,"PN amplitúdóval")
	p file u 1:2 t method1 w l ls 1, file u 1:3 t method2 w l ls 2
}

set output "waveMagnified.eps"
do for [file in files] {
	method=system("head -n 7 ".file." | tail -n 1")[header:*]
	amp=word(method,3)/2.0
	method1=sprintf("%s %g %s", word(method,1)." kölcsönhatás ",amp,"PN amplitúdóval")
	method=system("head -n 8 ".file." | tail -n 1")[header:*]
	amp=word(method,3)/2.0
	method2=sprintf("%s %g %s", word(method,1)." kölcsönhatás ",amp,"PN amplitúdóval")
	set multiplot
	set key inside left bottom vertical
	set origin 0.0,0.0; set size 1.0,1.0
	set xlabel "t[s]"; set xra[*:*]
	set ylabel "h[10^{-20}]"; set yra [-6.0e-20:+9.9e-20]
	p file u 1:2 t method1 w l ls 1, file u 1:3 t method2 w l ls 2

	unset key
	set origin 0.06,0.625; set size 0.8,0.35
	unset xlabel; set xra [2.00:*]
	unset ylabel; set ytics format "%3.1t"; set yra [-6.5e-20:+6.5e-20]
	p file u 1:2 t method1 w l ls 1, file u 1:3 t method2 w l ls 2
	unset multiplot; set origin 0.0,0.0; set size 1.0,1.0
}

# omega ===========================================================================================

# -- {
set output "omega.eps"
set multiplot
set key below
set xlabel "t[s]"; set xra [*:*]
set ylabel "{/Symbol w}_1/{/Symbol w}_2" rotate by 90; set ytics format "%g"; set yra [*:*]
plot for [index=1:words(files)] word(files,index) u 1:($11/$10) t getTitle(word(files,index)) w l ls index

unset key
set origin 0.1,0.2; set size 0.5,0.5
unset xlabel; set xrange [2.00:2.50]
unset ylabel; set yrange [0.95:1.00]
plot for [index=1:words(files)] word(files,index) u 1:($11/$10) w l ls index

unset multiplot; set origin 0.0,0.0; set size 1.0,1.0
# -- }

# -- {
set output "omegaDiff.eps"
set multiplot
set key below
set xra [*:*]
set ylabel "{/Symbol D}{/Symbol w}" rotate by 00; set ytics format "%g"; set yra [*:*]
plot for [index=1:words(files)] word(files,index) u 1:(abs($11-$10)) t getTitle(word(files,index)) w l ls index

unset key
set origin 0.1,0.4; set size 0.5,0.5
unset xlabel; set xrange [2.00:2.50]
unset ylabel; set yrange [*:0.04]
plot for [index=1:words(files)] word(files,index) u 1:(abs($11-$10)) w l ls index

unset multiplot; set origin 0.0,0.0; set size 1.0,1.0
# -- }

# phi =============================================================================================
set output "phi.eps"

set key below
set xlabel "t[s]"
set ylabel "{/Symbol f}" rotate by 0

# -- {
set multiplot
set xra [*:*]
set yra [*:*]
plot for [index=1:words(files)] word(files,index) u 1:($9/$8) t getTitle(word(files,index)) w l ls index

unset key
set origin 0.1,0.4; set size 0.5,0.5
unset xlabel; set xrange [2.40:*]
unset ylabel; set yrange [*:20.0]
plot for [index=1:words(files)] word(files,index) u 1:($9/$8) w l ls index

unset multiplot; set origin 0.0,0.0; set size 1.0,1.0
# -- }

